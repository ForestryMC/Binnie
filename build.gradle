buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = version_major + '.' + version_minor + '.' + version_patch
if (System.getenv("BUILD_NUMBER") != null) {
    version = version + "-" + System.getenv("BUILD_NUMBER")
}
group = "binnie" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "binnie-mods-1.11.2"

minecraft {
    version = mcversion + "-" + forgeversion
    mappings = mcp_mappings
    runDir = "run"
}

repositories {
    maven {
        name = "jei"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "ic2, forestry"
        url = "http://maven.ic2.player.to/"
    }
    ivy {
        name 'JEI Bees'
        artifactPattern "http://addons-origin.cursecdn.com/files/${jeibees_cf}/[module]-[revision]-[classifier].[ext]"
    }
}

dependencies {
    deobfCompile "net.sengir.forestry:forestry_${mcversion}:${forestry_version}"
    deobfCompile "mezz.jei:jei_${mcversion}:${jei_version}:api"
    runtime "mezz.jei:jei_${mcversion}:${jei_version}"
    deobfCompile name: 'jeibees', version: jeibees_version, ext: 'jar', classifier: "dev"
	deobfCompile "net.industrial-craft:industrialcraft-2:${ic2_version}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version,
                'mcversion': project.minecraft.version,
                'modid': "binniemods"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// prevent java 8's strict doclint for javadocs from failing builds
allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

uploadArchives {
    repositories {
        if (project.hasProperty('mavenDir')) {
            mavenDeployer {
                repository(url: "file://" + mavenDir)
            }
        } else {
            println 'Archives upload disabled, mavenDir in gradle.properties is missing.'
        }
    }
}
